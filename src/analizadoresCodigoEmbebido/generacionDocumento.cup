/*--------------- 1ra Area: Codigo de Usuario -----------------------*/
//-------> importaciones, paquetes

package backend.generacionHtml;
import java_cup.runtime.*;
import backend.arbolAST.*;
import backend.AccionesMatematicas.*;

import backend.asignacionCreacionDeVariables.*;
import backend.Escritura.*;
import backend.Etiquetas.*;
import backend.errores.*;
import backend.condiciones.*;
import backend.ciclos.*;
import backend.expresiones.*;
import backend.expresionesAritmeticas.*;
import backend.expesionesBooleanas.*;
import backend.expresionesConCadenas.*;
import backend.tablaDeSimbolos.*;
import frontend.manejadoresDeGui.ManejadorDeTextArea;
import java.util.ArrayList;


parser code {:

    private ManejadorDeTextArea manejadorDeTextArea;
    private ArrayList<Variable> tablaDeSimbolos;
    private ArrayList<ErrorAnalisis> erroresEnAnalisis;
    private ManejadorDeTablaDeSimbolos manejadorDeVariables;//----->Quizas no lo necesitemos
    ArrayList<Nodo> instruccionesParaIf;
    ArrayList<Nodo> instruccionesElseParaIf;
    ArrayList<Nodo> instruccionesPrint;
    ArrayList<String> textoAEscribir;
    ArrayList<AtributoDeEtiqueta> atributos;
    ArrayList<Etiqueta> etiquetas;

    // Connect this parser to a scanner!
    public parser(AnalizadorDeCodigoEmbebido lex,ArrayList<Variable> tablaDeSimbolos,ManejadorDeTextArea manejadorDeTextArea,ArrayList<ErrorAnalisis> erroresEnAnalisis) {
	super(lex);
        this.manejadorDeTextArea=manejadorDeTextArea;
        this.tablaDeSimbolos=tablaDeSimbolos;
        this.manejadorDeVariables = new ManejadorDeTablaDeSimbolos(tablaDeSimbolos);
        this.erroresEnAnalisis=erroresEnAnalisis;
        
        
        this.instruccionesParaIf=new ArrayList<>();
        this.instruccionesElseParaIf=new ArrayList<>();
        this.instruccionesPrint=new ArrayList<>();
        this.textoAEscribir=new ArrayList<>();
        this.atributos=new ArrayList<>();
        this.etiquetas=new ArrayList<>();
    }

//Metodo al que se llama automaticamente ante algun error sintactico
    public void syntax_error(Symbol s){        
        System.out.println("ERROR EN ESTRUCTURA:"+s.value.toString()+" "+s.left+" "+s.right);
        String lexema = s.value.toString();
        int linea = s.left;
        int columna = s.right;
        ErrorSintactico nuevoError = new ErrorSintactico(linea,columna,"Error sintactico,token:"+lexema);
        erroresEnAnalisis.add(nuevoError);
}

    //Metodo al que se llama en el momento en que ya no es posible una recuperacion de errores
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{        
                //System.out.println("ERROR EN ESTRUCTURA:"+s);
    }



:}


/*--------------- 2da Area: Declaraciones -----------------------*/

//---------------------------------------------TERMINALES-----------------------------------
/****************************************HTML Y OTROS*************************/

terminal String E_HTML,
E_HEAD,
E_TITLE,
E_BODY,
E_CENTER,
E_PARRAFO,
E_NEGRILLA,
E_SUBRAYADO,
E_ITALICA,
E_STRIKE,
E_BLINK,
E_SUB,
E_SUP,
E_FONT,
E_UL,
E_LI,
E_OL,
E_BR,
E_NOBR,
E_HR,
E_BLOCKQUOTE,
E_A,
E_HREF;

terminal String BGCOLOR,
TEXT,
LINK,
ALIGN,
SIZE,
FACE,
COLOR,
TYPE,
START,
WIDTH,
NAME;

terminal String LT,
GT,
AMP,
AACUTE,
EACUTE,
IACUTE,
OACUTE,
UACUTE,
NTILDEM,
NTILDE;

terminal String ORACION_TIPO_ATRIBUTO;

/****************************************Expresiones*************************/

terminal String NUMERO;


/****************************************Definición de variables*************************/

terminal String     VAR,IDENTIFICADOR;

terminal String     INTEGER,BOOLEAN,STRING,TRUE,FALSE;


/****************************************Operaciones logicas*************************/

terminal String     AND,OR;

/****************************************Condicional(If)*************************/

terminal String     IF,THEN,ELSE;

/***************************************Ciclos***********************************/

terminal String     WHILE,FOR,TO;

/***************************************Escritura***********************************/

terminal String     PRINT,ORACION,URL;

/***************************************Bloques***********************************/

terminal String     BEGIN,END;



/****************************************Definición de simbolos*************************/

terminal String         MAS,MENOS,POR,DIV,MAYOR,MENOR,DOS_PUNTOS,IGUAL,PARENTESIS_ABIERTO,PARENTESIS_CERRADO,
                        COMA,PUNTO_COMA,CORCHETE_ABIERTO,CORCHETE_CERRADO,PORCENTAJE;//Expresiones matematicas





//--------------------------------------------NO TERMINALES-------------------------------------

/****************************************Condiconal***********************/

non terminal String             cicloWhile,cicloFor,condicional;
non terminal ArrayList<Nodo>    bloque,conjuntoDeInstruccionesComplejas;
non terminal Nodo               unaInstruccionCompleja;

/****************************************Asignaciones***********************/

non terminal String declaracionDeVariable,asignacionDeVariable;

/****************************************Escritura*************************/
non terminal String  escritura;
non terminal Nodo unTexto,ultimaInstruccion;
non terminal ArrayList<Nodo> conjuntoDeTexto;
/****************************************Expresiones ***********************/
non terminal Expresion     expresion,t,f; //Expresiones matematicas y cadenas

non terminal Expresion        expresionesBooleanas,exB,exB1;//Expresiones booleanas
/****************************************Expresiones Con cadenas***********************/

non terminal ExpresionConCadena     expresionCadena,cadena,tipoDeCadena;



non terminal String             conjuntoDeInstrucciones,unaInstruccion;

/****************************************HTML***********************/

non terminal String     conjuntoDeEtiquetas,etiqueta,texto,atributos,unAtributo,etiquetaNoAnidable;
non terminal Object     atributosParrafo;
non terminal String     p,p1,p2;
non terminal String     s1,s2;
non terminal String     etiquetaBody,etiquetaParrafo,etiquetaFont,etiquetaHR;
non terminal String     unAtributoBody,unAtributoFont,unAtributosHr;
non terminal String   atributosBody,atributosFont,atributosHR;
non terminal String    conjuntoDeTexto2;
non terminal String               texto2;
/*--------------- 3ra Area: Reglas Semanticas -----------------------*/

precedence left OR;
precedence left AND;


start with p;


//-----------------------------------------HTML-----------------------------------------

                                            //Encabezado//
p::=MENOR E_HTML MAYOR p1:e MENOR DIV E_HTML MAYOR//Con head
    {:String total="<html>"+e+"</html>";
    System.out.println("******************************************");
    System.out.println(total);
    System.out.println("******************************************");
:};
p::=MENOR E_HTML MAYOR p2:e MENOR DIV E_HTML MAYOR
    {:String total="<html>"+e+"</html>";
    System.out.println("******************************************");
    System.out.println(total);
    System.out.println("******************************************");
:};//Sin head
p::=error;   

                                         //Head//
p1::=MENOR E_HEAD MAYOR MENOR DIV E_HEAD MAYOR p2:i
     {:RESULT="<head></head>"+i;:};
p1::=MENOR E_HEAD MAYOR MENOR E_TITLE MAYOR conjuntoDeTexto2:i MENOR DIV E_TITLE MAYOR MENOR DIV E_HEAD MAYOR p2:e
    {:RESULT="<head><title>"+i+"</title></head>"+e+"\n";:};

                                           //Body//
p2::=MENOR etiquetaBody:i MAYOR s1:e MENOR DIV E_BODY MAYOR{:RESULT="<"+i+">"+e+"</body>";:}
    |error;
    
s1::=s1:e MENOR etiquetaParrafo:i MAYOR s2:f MENOR DIV E_PARRAFO MAYOR{:RESULT=e+"<"+i+">"+f+"</p>\n";:}|
        MENOR etiquetaParrafo:i MAYOR s2:f MENOR DIV E_PARRAFO MAYOR{:RESULT="<"+i+">"+f+"</p>\n";:}|
     s1:e MENOR E_CENTER MAYOR s2:f MENOR DIV E_CENTER MAYOR{:RESULT=e+"<center>"+f+"</center>\n";:}|
        MENOR E_CENTER MAYOR s2:f MENOR DIV E_CENTER MAYOR{:RESULT="<center>"+f+"</center>\n";:}|
     s1:e MENOR etiquetaFont:i MAYOR s2:f MENOR DIV E_FONT MAYOR{:RESULT=e+"<"+i+">"+f+"</font>\n";:}|
        MENOR etiquetaFont:i MAYOR s2:f MENOR DIV E_FONT MAYOR{:RESULT="<"+i+">"+f+"</font>\n";:}|
     s1:e MENOR E_BR MAYOR s2:f MENOR DIV E_BR MAYOR{:RESULT=e+"<br>"+f+"</br>\n";:}|
        MENOR E_BR MAYOR s2:f MENOR DIV E_BR MAYOR{:RESULT="<br>"+f+"</br>\n";:}|
     s1:e MENOR E_NOBR MAYOR s2:f MENOR DIV E_NOBR MAYOR{:RESULT=e+"<nobr>"+f+"</nobr>\n";:}|
        MENOR E_NOBR MAYOR s2:f MENOR DIV E_NOBR MAYOR{:RESULT="<nobr>"+f+"</nobr>\n";:}|
     s1:e MENOR PORCENTAJE  conjuntoDeInstrucciones:i PORCENTAJE MAYOR
        {:RESULT=e+i;:}|
        MENOR PORCENTAJE  conjuntoDeInstrucciones:i PORCENTAJE MAYOR
        {:RESULT=i;:}|
        error;

s2::=s2:e MENOR E_NEGRILLA MAYOR s2:i MENOR DIV E_NEGRILLA MAYOR{:RESULT=e+"<b>"+i+"</b>";:}|
        MENOR E_NEGRILLA MAYOR s2:i MENOR DIV E_NEGRILLA MAYOR{:RESULT="<b>"+i+"</b>";:}|
     s2:e MENOR E_SUBRAYADO MAYOR s2:i MENOR DIV E_SUBRAYADO MAYOR{:RESULT=e+"<u>"+i+"</u>";:}|
        MENOR E_SUBRAYADO MAYOR s2:i MENOR DIV E_SUBRAYADO MAYOR{:RESULT="<u>"+i+"</u>";:}|
     s2:e MENOR E_ITALICA MAYOR s2:i MENOR DIV E_ITALICA MAYOR{:RESULT=e+"<i>"+i+"</i>";:}|
        MENOR E_ITALICA MAYOR s2:i MENOR DIV E_ITALICA MAYOR{:RESULT="<i>"+i+"</i>";:}|
     s2:e MENOR E_STRIKE MAYOR s2:i MENOR DIV E_STRIKE MAYOR{:RESULT=e+"<strike>"+i+"</strike>";:}|
        MENOR E_STRIKE MAYOR s2:i MENOR DIV E_STRIKE MAYOR{:RESULT="<strike>"+i+"</strike>";:}|
     s2:e MENOR E_BLINK MAYOR s2:i MENOR DIV E_BLINK MAYOR{:RESULT=e+"<blink>"+i+"</blink>";:}|
        MENOR E_BLINK MAYOR s2:i MENOR DIV E_BLINK MAYOR{:RESULT="<blink>"+i+"</blink>";:}|
     s2:e MENOR E_SUB MAYOR s2:i MENOR DIV E_SUB MAYOR{:RESULT=e+"<sub>"+i+"</sub>";:}|
        MENOR E_SUB MAYOR s2:i MENOR DIV E_SUB MAYOR{:RESULT="<sub>"+i+"</sub>";:}|        
     s2:e MENOR E_SUP MAYOR s2:i MENOR DIV E_SUP MAYOR{:RESULT=e+"<sup>"+i+"</sup>";:}|
        MENOR E_SUP MAYOR s2:i MENOR DIV E_SUP MAYOR{:RESULT="<sup>"+i+"</sup>";:}|
     s2:e MENOR E_BLOCKQUOTE MAYOR s2:i MENOR DIV E_BLOCKQUOTE MAYOR{:RESULT=e+"<blockquote>"+i+"</blockquote>";:}|
        MENOR E_BLOCKQUOTE MAYOR s2:i MENOR DIV E_BLOCKQUOTE MAYOR{:RESULT="<blockquote>"+i+"</blockquote>";:}|
     s2:e MENOR E_A NAME IGUAL ORACION_TIPO_ATRIBUTO:i MAYOR s2:f MENOR DIV E_A MAYOR
         {:RESULT=e+"<a name ="+i+">"+f+"</a>";:}|
        MENOR E_A NAME IGUAL ORACION_TIPO_ATRIBUTO:i MAYOR s2:f MENOR DIV E_A MAYOR
        {:RESULT="<a name ="+i+">"+f+"</a>";:}|
     s2:e MENOR E_HREF ORACION_TIPO_ATRIBUTO:i MAYOR s2:f MENOR DIV E_HREF MAYOR
        {:RESULT=e+"<a href ="+i+">"+f+"</a>";:}|
        MENOR E_HREF ORACION_TIPO_ATRIBUTO:i MAYOR s2:f MENOR DIV E_HREF MAYOR
        {:RESULT="<a name ="+i+">"+f+"</a>";:}|
     s2:e MENOR etiquetaHR:i MAYOR s2:f MENOR DIV E_HR MAYOR
        {:RESULT=e+"<"+i+">"+f+"</hr>";:}|
        MENOR etiquetaHR:i MAYOR s2:f MENOR DIV E_HR MAYOR
        {:RESULT="<"+i+">"+f+"</hr>";:}|
     
    s2:e MENOR PORCENTAJE conjuntoDeInstrucciones:i PORCENTAJE MAYOR
         {:RESULT=e+i;:}|
        MENOR PORCENTAJE conjuntoDeInstrucciones:i PORCENTAJE MAYOR
         {:RESULT=i;:}|
         error;


                                        //Parrafo
etiquetaParrafo::=E_PARRAFO:i{:RESULT=i;:} |
           E_PARRAFO:i ALIGN:e IGUAL:f ORACION_TIPO_ATRIBUTO:g{:RESULT=i+e+f+g;:}; 

etiquetaBody::=E_BODY:i {:RESULT=i;:};
etiquetaBody::=E_BODY:i atributosBody:e{:RESULT=i+e;:};
atributosBody::=atributosBody:i unAtributoBody:e{:RESULT=i+e;:}|unAtributoBody:i{:RESULT=i;:};
unAtributoBody::= BGCOLOR IGUAL ORACION_TIPO_ATRIBUTO:i{:RESULT="bgcolor ="+i+" ";:};
unAtributoBody::= TEXT IGUAL ORACION_TIPO_ATRIBUTO:i{:RESULT="text ="+i+" ";:};
unAtributoBody::= LINK IGUAL ORACION_TIPO_ATRIBUTO:i{:RESULT="link ="+i+" ";:};

etiquetaFont::=E_FONT:i{:RESULT=i;:};
etiquetaFont::=E_FONT:i atributosFont:e{:RESULT=i+e;:};
atributosFont::=atributosFont:i unAtributoFont:e{:RESULT=i+e;:} | unAtributoFont:i{:RESULT=i;:};
atributosFont::=SIZE IGUAL ORACION_TIPO_ATRIBUTO:i{:RESULT="size ="+i+" ";:};
atributosFont::=ALIGN IGUAL ORACION_TIPO_ATRIBUTO:i{:RESULT="align ="+i+" ";:};
atributosFont::=FACE IGUAL ORACION_TIPO_ATRIBUTO:i{:RESULT="face ="+i+" ";:};
atributosFont::=COLOR IGUAL ORACION_TIPO_ATRIBUTO:i{:RESULT="color ="+i+" ";:};


etiquetaHR::=E_HR:i{:RESULT=i;:};
etiquetaHR::=E_HR:i atributosHR:e{:RESULT=i+e;:};
atributosHR::=atributosHR:i unAtributosHr:e{:RESULT=i+e;:}
|unAtributosHr:i{:RESULT=i;:};
unAtributosHr::=ALIGN IGUAL ORACION_TIPO_ATRIBUTO:i{:RESULT="align="+i;:};
unAtributosHr::=WIDTH IGUAL ORACION_TIPO_ATRIBUTO:i{:RESULT="width="+i;:};
unAtributosHr::=SIZE IGUAL ORACION_TIPO_ATRIBUTO:i{:RESULT="size=+i";:};


s2::=conjuntoDeTexto2:i{:RESULT=i;:};
                                            //Texto//
conjuntoDeTexto2::= conjuntoDeTexto2:i texto2:e
                    {:RESULT=i+e;
                    :}
                    |texto2:e
                     {:
                     RESULT=e;
                     :};

texto2::=IDENTIFICADOR:i{:RESULT=i;:};
texto2::=DOS_PUNTOS:i{:RESULT=i;:};
texto2::=LT:i{:RESULT=i;:}|GT:i{:RESULT=i;:}|AMP:i{:RESULT=i;:}|AACUTE:i{:RESULT=i;:}|EACUTE:i{:RESULT=i;:}|
IACUTE:i{:RESULT=i;:}|OACUTE:i{:RESULT=i;:}|UACUTE:i{:RESULT=i;:}|NTILDEM:i{:RESULT=i;:}|NTILDE:i{:RESULT=i;:};
/*
texto2::=BGCOLOR:i{:RESULT=i;:}|TEXT:i{:RESULT=i;:}|LINK:i{:RESULT=i;:}|ALIGN:i{:RESULT=i;:}|
SIZE:i{:RESULT=i;:}|FACE:i{:RESULT=i;:}|COLOR:i{:RESULT=i;:}|TYPE:i{:RESULT=i;:}
|START:i{:RESULT=i;:}|WIDTH:i{:RESULT=i;:}|NAME:i{:RESULT=i;:};

texto2::=E_HTML:i{:RESULT=i;:}|E_HEAD:i{:RESULT=i;:}|E_TITLE:i{:RESULT=i;:}|E_BODY:i{:RESULT=i;:}|E_CENTER:i{:RESULT=i;:}|E_PARRAFO:i{:RESULT=i;:}|
E_NEGRILLA:i{:RESULT=i;:}|E_SUBRAYADO:i{:RESULT=i;:}|E_ITALICA:i{:RESULT=i;:}|E_STRIKE:i{:RESULT=i;:}|E_BLINK:i{:RESULT=i;:}|
E_SUB:i{:RESULT=i;:}|E_SUP:i{:RESULT=i;:}|E_FONT:i{:RESULT=i;:}|E_UL:i{:RESULT=i;:}|E_LI:i{:RESULT=i;:}|E_OL:i{:RESULT=i;:}|
E_BR:i{:RESULT=i;:}|E_NOBR:i{:RESULT=i;:}|E_HR:i{:RESULT=i;:}|E_BLOCKQUOTE:i{:RESULT=i;:}|E_A:i{:RESULT=i;:}|E_HREF:i{:RESULT=i;:};
*/
//-----------------------------------------Instrucciones posibles-----------------------------------------

conjuntoDeInstrucciones::= conjuntoDeInstrucciones:i unaInstruccion:e {:RESULT=i+e;:} | unaInstruccion:i{:RESULT=i;:};
unaInstruccion::=declaracionDeVariable{:RESULT="";:};
unaInstruccion::=asignacionDeVariable{:RESULT="";:};
unaInstruccion::=condicional:i{:RESULT=i;:};
unaInstruccion::=escritura:i{:RESULT=i;:};
unaInstruccion::=cicloFor:i{:RESULT=i;:};
unaInstruccion::=cicloWhile:i{:RESULT=i;:}|
error;

 //----------------------------------------------------CicloWhile(While)-------------------------------

cicloWhile::=WHILE:i expresionesBooleanas:e bloque:f
             {:CicloWhile cicloWhile = new CicloWhile(ileft,iright,e,f);
               ManejadorDeCicloWhile manW= new ManejadorDeCicloWhile(cicloWhile,manejadorDeVariables,erroresEnAnalisis);
               manW.realizarOperaciones();
               RESULT=cicloWhile.getTexto();
             :};

cicloWhile::=WHILE:i IDENTIFICADOR:e bloque:f
             {:Identificador id=new Identificador(eleft,eright,e);
               CicloWhile cicloWhile = new CicloWhile(ileft,iright,id,f);
               ManejadorDeCicloWhile manW= new ManejadorDeCicloWhile(cicloWhile,manejadorDeVariables,erroresEnAnalisis);
               manW.realizarOperaciones();
               RESULT=cicloWhile.getTexto();
               :};


 //----------------------------------------------------CicloFor(For)-------------------------------

cicloFor::=FOR:i IDENTIFICADOR:e DOS_PUNTOS IGUAL NUMERO:f TO NUMERO:g bloque:h
            {:CicloFor cicloF = new CicloFor(ileft,iright,f,g,h,e,manejadorDeVariables);
            ManejadorDeCicloFor man=new ManejadorDeCicloFor(cicloF,manejadorDeVariables,erroresEnAnalisis);
            man.realizarOperaciones();
            RESULT=cicloF.getTexto();
            :};

 //----------------------------------------------------Condicional(IF)-------------------------------
condicional::=IF:i IDENTIFICADOR:e THEN bloque:f ELSE bloque:g
              {:
              Identificador id=new Identificador(eleft,eright,e);
              Condicion condicion= new Condicion(ileft,iright,id,f,g);
              ManejadorDeCondiciones manejador = new ManejadorDeCondiciones(condicion,manejadorDeVariables,erroresEnAnalisis);
              manejador.realizarOperaciones();
              //instruccionesParaIf=new ArrayList<>();
              //instruccionesElseParaIf=new ArrayList<>();
              RESULT=condicion.getTexto();
              :};


condicional::=IF:i expresionesBooleanas:e THEN bloque:f ELSE bloque:g
                {:
                Condicion condicion= new Condicion(ileft,iright,e,f,g);
                ManejadorDeCondiciones manejador = new ManejadorDeCondiciones(condicion,manejadorDeVariables,erroresEnAnalisis);
                manejador.realizarOperaciones();
                RESULT=condicion.getTexto();
                :};


 //----------------------------------------------------Bloque-------------------------------

bloque::= BEGIN CORCHETE_ABIERTO conjuntoDeInstruccionesComplejas:i CORCHETE_CERRADO END PUNTO_COMA
          {:RESULT=i;:};

bloque::=unaInstruccionCompleja:i
         {:ArrayList<Nodo> lista=new ArrayList<>();
           lista.add(i);
           RESULT=lista;  :};


 //----------------------------------------------------Instrucciones para guardar en lista-------------------------------
                    
conjuntoDeInstruccionesComplejas::=conjuntoDeInstruccionesComplejas:i unaInstruccionCompleja:e
{:
    i.add(e);
    RESULT=i;
:};
conjuntoDeInstruccionesComplejas::=unaInstruccionCompleja:i
                                    {:ArrayList<Nodo> lista=new ArrayList<>();
                                     lista.add(i);
                                     RESULT=lista;   
                                    :};

unaInstruccionCompleja::= VAR:a IDENTIFICADOR:i DOS_PUNTOS INTEGER PUNTO_COMA
                          {:RESULT=new Variable(ileft,iright,i,"entera","0");:}|
                          VAR IDENTIFICADOR:i DOS_PUNTOS BOOLEAN:e PUNTO_COMA
                          {:RESULT=new Variable(ileft,iright,i,"booleana","true");:}|
                          VAR IDENTIFICADOR:i DOS_PUNTOS STRING:e PUNTO_COMA
                          {:RESULT=new Variable(ileft,iright,i,"cadena","");:};

unaInstruccionCompleja::=IDENTIFICADOR:i DOS_PUNTOS IGUAL expresion:e PUNTO_COMA
                         {:RESULT=new Asignacion(i, e, manejadorDeVariables);:}|
                         IDENTIFICADOR:i DOS_PUNTOS IGUAL expresionesBooleanas:e PUNTO_COMA
                         {:RESULT=new Asignacion(i, e, manejadorDeVariables);:};


unaInstruccionCompleja::=IF:i IDENTIFICADOR:e THEN bloque:f ELSE bloque:g
{:System.out.println("ACCION SI");Identificador id=new Identificador(eleft,eright,e);
                        Condicion condicion= new Condicion(ileft,iright,id,f,g);
                        RESULT =condicion;
                        :};


unaInstruccionCompleja::=IF:i expresionesBooleanas:e THEN bloque:f ELSE bloque:g
                        {:
                        System.out.println("Accion con booleanas");
                        Condicion condicion= new Condicion(ileft,iright,e,f,g);
                        RESULT =condicion;
                        :};


unaInstruccionCompleja::=PRINT:i PARENTESIS_ABIERTO conjuntoDeTexto:e ultimaInstruccion:f PARENTESIS_CERRADO PUNTO_COMA
           {:
            e.add(f);
            Escritura escritura=new Escritura(ileft,iright,e);
            RESULT=escritura;
            :};

unaInstruccionCompleja::=PRINT:i PARENTESIS_ABIERTO ultimaInstruccion:e PARENTESIS_CERRADO PUNTO_COMA
            {:
            ArrayList<Nodo> lista = new ArrayList<>();
            lista.add(e);
            Escritura escritura=new Escritura(ileft,iright,lista);
            RESULT =escritura;
            :};

unaInstruccionCompleja::=FOR:i IDENTIFICADOR:e DOS_PUNTOS IGUAL NUMERO:f TO NUMERO:g bloque:h
            {:CicloFor cicloF = new CicloFor(ileft,iright,f,g,h,e,manejadorDeVariables);
              RESULT =cicloF;
            :};

unaInstruccionCompleja::=WHILE:i expresionesBooleanas:e bloque:f
             {:CicloWhile cicloWhile = new CicloWhile(ileft,iright,e,f);
                RESULT = cicloWhile;
             :};
unaInstruccionCompleja::=WHILE:i IDENTIFICADOR:e bloque:f
              {:Identificador id=new Identificador(eleft,eright,e);
               CicloWhile cicloWhile = new CicloWhile(ileft,iright,id,f);
               RESULT=cicloWhile;
              :};

 //----------------------------------------------------Escritura-------------------------------
escritura::=PRINT:i PARENTESIS_ABIERTO conjuntoDeTexto:e ultimaInstruccion:f PARENTESIS_CERRADO PUNTO_COMA
           {:
            e.add(f);
            Escritura escritura=new Escritura(ileft,iright,e);
             ManejadorDeEscritura  man =new ManejadorDeEscritura(escritura,manejadorDeVariables,erroresEnAnalisis);
             man.manejarEscritura();
             RESULT=escritura.getTextoGenerado();
            :};

escritura::=PRINT:i PARENTESIS_ABIERTO ultimaInstruccion:e PARENTESIS_CERRADO PUNTO_COMA
            {:System.out.println("PRINT RECONOCIDO");
            ArrayList<Nodo> lista = new ArrayList<>();
            lista.add(e);
            Escritura escritura=new Escritura(ileft,iright,lista);
            ManejadorDeEscritura  man =new ManejadorDeEscritura(escritura,manejadorDeVariables,erroresEnAnalisis);
            man.manejarEscritura();
            :};

conjuntoDeTexto::=conjuntoDeTexto:i unTexto:e
                  {:i.add(e);
                    RESULT=i;
                  :};
conjuntoDeTexto::=unTexto:e
                  {:ArrayList<Nodo> lista = new ArrayList<>();
                    lista.add(e);
                    RESULT=lista;
                   :};

unTexto::=ORACION:i COMA{:RESULT=new Oracion(ileft,iright,i);:};
unTexto::=IDENTIFICADOR:i COMA{:RESULT =new Identificador(ileft, iright,i);:};
ultimaInstruccion::=ORACION:i{:RESULT =new Oracion(ileft,iright,i);:};
ultimaInstruccion::=IDENTIFICADOR:i{:RESULT=new Identificador(ileft, iright,i);:};


 //----------------------------------------------------Declaracion de variables-------------------------------

declaracionDeVariable::=VAR:a IDENTIFICADOR:i DOS_PUNTOS INTEGER PUNTO_COMA
                        {:Variable variable = new Variable(ileft,iright,i,"entera","0");
                        ManejadorDeCreacionDeVariables man = new ManejadorDeCreacionDeVariables(manejadorDeVariables,variable,erroresEnAnalisis);
                        man.crearVariable();:}|
                        VAR IDENTIFICADOR:i DOS_PUNTOS BOOLEAN:e PUNTO_COMA
                        {:Variable variable = new Variable(ileft,iright,i,"booleana","true");
                        ManejadorDeCreacionDeVariables man = new ManejadorDeCreacionDeVariables(manejadorDeVariables,variable,erroresEnAnalisis);
                        man.crearVariable();:}|
                        VAR IDENTIFICADOR:i DOS_PUNTOS STRING:e PUNTO_COMA
                        {:Variable variable = new Variable(ileft,iright,i,"cadena","");
                        ManejadorDeCreacionDeVariables man = new ManejadorDeCreacionDeVariables(manejadorDeVariables,variable,erroresEnAnalisis);
                        man.crearVariable();:};

//----------------------------------------------------Asignacion de variables-------------------------------

asignacionDeVariable::= IDENTIFICADOR:i DOS_PUNTOS IGUAL expresion:e PUNTO_COMA
                        {:Asignacion asignacion = new Asignacion(i, e, manejadorDeVariables);
                        ManejadorDeAsignacionDeExpresiones nuevaAsignacion = new ManejadorDeAsignacionDeExpresiones(manejadorDeVariables, asignacion,true,erroresEnAnalisis);
                        nuevaAsignacion.asignacionDeVariable();:}|
                        IDENTIFICADOR:i DOS_PUNTOS IGUAL expresionesBooleanas:e PUNTO_COMA
                        {:System.out.println("Asignacion a booleana");
                        Asignacion asignacion = new Asignacion(i, e, manejadorDeVariables);
                        ManejadorDeAsignacionDeExpresiones nuevaAsignacion = new ManejadorDeAsignacionDeExpresiones(manejadorDeVariables, asignacion,true,erroresEnAnalisis);
                        nuevaAsignacion.asignacionDeVariable();:};    
                        

 //----------------------------------------------------EXPRESIONES CON BOOLEANOS-------------------------------

expresionesBooleanas::= expresionesBooleanas:i OR  expresion:e
{:RESULT=new OperacionBooleana(ileft,iright,"or", i,e);:};

expresionesBooleanas::= expresionesBooleanas:i AND  expresion:e
{:RESULT=new OperacionBooleana(ileft,iright,"and", i,e);:};


expresionesBooleanas::= expresionesBooleanas:i OR expresionesBooleanas:e
{:RESULT=new OperacionBooleana(ileft,iright,"or",i,e);:};

expresionesBooleanas::= expresionesBooleanas:i AND expresionesBooleanas:e
{:RESULT=new OperacionBooleana(ileft,iright,"and", i,e);:};

expresionesBooleanas::=expresion:i OR expresion:e
{:RESULT=new OperacionBooleana(ileft,iright,"or", i,e);:};

expresionesBooleanas::=expresion:i AND expresion:e
{:RESULT=new OperacionBooleana(ileft,iright,"and", i,e);:};

expresionesBooleanas::=expresion:i OR expresionesBooleanas:e
{:RESULT=new OperacionBooleana(ileft,iright,"or", i,e);:};

expresionesBooleanas::=expresion:i AND expresionesBooleanas:e
{:RESULT=new OperacionBooleana(ileft,iright,"and", i,e);:};


expresionesBooleanas::=expresion:i MAYOR expresion:e
{:RESULT= new Comparacion(ileft,iright,">",i,e);:};

expresionesBooleanas::=expresion:i MENOR expresion:e
{:RESULT= new Comparacion(ileft,iright,"<",i,e);:};

expresionesBooleanas::=expresion:i MAYOR IGUAL expresion:e
{:RESULT= new Comparacion(ileft,iright,">=",i,e);:};

expresionesBooleanas::=expresion:i MENOR IGUAL expresion:e
{:RESULT= new Comparacion(ileft,iright,"<=",i,e);:};

expresionesBooleanas::=expresion:i IGUAL expresion:e
{:RESULT= new Comparacion(ileft,iright,"=",i,e);:};

expresionesBooleanas::=expresion:i MENOR MAYOR expresion:e
{:RESULT= new Comparacion(ileft,iright,"<>",i,e);:};



/*
expresionesBooleanas::=exB;

exB::=  exB AND exB1;

exB::=  exB1;

exB1::= expresion MAYOR expresion;

exB1::= expresion MENOR expresion;

exB1::= expresion MAYOR IGUAL expresion;

exB1::= expresion MENOR IGUAL expresion;

exB1::= expresion MAYOR MENOR expresion;

exB1::= IDENTIFICADOR;
*/
 //----------------------------------------------------EXPRESIONES MATEMATICAS Y CON CADENAS-------------------------------



expresion::=expresion:i MAS t:e
    {:RESULT= new OperacionBinaria(iright, ileft, i, e,"+");:};

expresion::=expresion:i MENOS t:e
    {:RESULT= new OperacionBinaria(iright, ileft, i, e,"-");:};

expresion::=t:i
    {:RESULT=i;:};

t::=t:i POR f:e
    {:RESULT= new OperacionBinaria(iright, ileft, i, e,"*");:};

t::=t:i DIV f:e
    {:RESULT= new OperacionBinaria(iright, ileft, i, e,"/");:};


t::=f:i
    {:RESULT=i;:};

f::=IDENTIFICADOR:i//Se debe buscar el identificador, y si se encuenra RESULT=valor de lo contrario mostrar error
    {:RESULT=new Identificador(ileft,iright,i);:};

f::=NUMERO:i
    {:RESULT= new Operando(iright,ileft, i);:};

f::= ORACION:i
     {:RESULT=new Cadena(ileft,iright,i);:};

f::=TRUE:i
    {:RESULT= new ValorDeVerdad(ileft,iright,"true");:};

f::=FALSE:i
    {:RESULT= new ValorDeVerdad(ileft,iright,"false");:};

